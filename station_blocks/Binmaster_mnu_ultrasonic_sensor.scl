FUNCTION_BLOCK "Binmaster_mnu_ultrasonic_sensor"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      mb_addr : UInt;
   END_VAR

   VAR_IN_OUT 
      mb_query : "mb_query";
   END_VAR

   VAR 
      s : "mb_station_udt";
      data : Struct
         "Model Type" : UInt;
         Raw_Distance_Level : UInt;   // [mm]
         "_" : UInt;
         Temperature : Int;
         Calculated : DInt;
         "__" : DWord;
         "Version" : UInt;
         "Signal Strength" : UInt;
         "___" : UInt;
         "Trip 1 Alarm" : UInt;
         "Trip 1 Status" : UInt;
         "Trip 2 Alarm" : UInt;
         "Trip 2 Status" : UInt;
      END_STRUCT;
      setting : Struct
         Units : UInt := 3;   // 3=Meters
         Distance : Struct
            Max : UInt;   // 0 - 15,250 mm
            Full : UInt;   // 0 - 15,250 mm
            Empty : UInt;
         END_STRUCT;
         cal : Struct
            Multiplier : UInt := 1000;   // 1000 -> 1.000
            Offset : UInt := 0;
         END_STRUCT;
      END_STRUCT;
   END_VAR


BEGIN
	// Binmaster - MNU Ultrasonic Sensor
	// 
	// Web: http://www.binmaster.com/_resources/e30d:ogspbx-on/files/
	// 75651567z4d4b98f5/_fn/BinMaster%20MNU%20manual%209004257%20Rev%20A1.pdf
	
	"mb_station_header"(station := #s, mb_query := #mb_query);
	#mb_query.mb_addr := #mb_addr;
	
	#mb_query.mode := #mb_query.c.mode.read;
	#mb_query(data_addr := 30299, data_ptr := #data);
	
	#mb_query.mode := #mb_query.c.mode.write;
	#mb_query(data_addr := 40401, data_ptr := #setting.Units);
	#mb_query(data_addr := 40405, data_ptr := #setting.Distance);
	#mb_query(data_addr := 40416, data_ptr := #setting.cal);
	
	"mb_station_footer"(station := #s, mb_query := #mb_query);
	
END_FUNCTION_BLOCK

