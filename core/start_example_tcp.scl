FUNCTION_BLOCK "siemens_PAC3200"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      unit : USInt;
   END_VAR

   VAR_IN_OUT 
      mb : "udt_mb";
   END_VAR

   VAR 
      common : "udt_mb_common";
      current : Array[1..3] of Real;
      misc : Struct
         frequency : Real;   // unit: hz
         avg_voltage_Vph_n : Real;   // unit: V
         avg_voltage_Vph_ph : Real;   // unit: V
         avg_current : Real;   // unit: A
         total_apparent_power : Real;   // unit: VA
         total_active_power : Real;   // unit: W
      END_STRUCT;
   END_VAR


BEGIN
	// - Siemens - PAC3200
	// - http://w3.siemens.com.br/buildingtechnologies/br/documents/manual-sentron-pac3200.pdf
	//   3.9.3, page 40
	
	// - Example code, no test with real equipment done.
	
	"mb_device_header"(common := #common, mb := #mb);
	
	"mb_query"(unit := #unit,
	           fc := #mb.c.read.holding_reg,
	           d_addr := 13,
	           d_len := #mb.c.auto_len,
	           data := #current,
	           mb := #mb );
	
	"mb_query"(unit := #unit,
	           fc := #mb.c.read.holding_reg,
	           d_addr := 55,
	           d_len := #mb.c.auto_len,
	           data := #misc,
	           mb := #mb );
	
	"mb_device_footer"(common := #common, mb := #mb);
	
	IF #common.out.communication_error THEN
	    UFILL_BLK(IN := 0,
	              COUNT := 3,
	              OUT => #current[1]);
	END_IF;
	
END_FUNCTION_BLOCK

FUNCTION_BLOCK "start_example_mb_tcp"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR 
      mb : "udt_mb";
      mb_client_ctrl : "mb_client_ctrl";
      siemens_PAC3200 : "siemens_PAC3200";
   END_VAR


BEGIN
	// interface:  Profinet interface. (See "hardware configuration")
	// conn_id:    A unique id for each tcp connectin in the plc-program.
	// ip_addr:    The ip-address of the server. (Array of 4 bytes)
	// tcp_port:   TCP-port. 502 is the default port. UDP isn't available.
	// timeout:    Time datatype
	// mb:         A udt that link the queries to a mb_client_ctrl.
	#mb_client_ctrl.ip_addr[1] := 192;
	#mb_client_ctrl.ip_addr[2] := 168;
	#mb_client_ctrl.ip_addr[3] := 1;
	#mb_client_ctrl.ip_addr[4] := 2;
	#mb_client_ctrl(interface := "Local~PROFINET_interface_1",
	                conn_id := 123,
	                tcp_port := 502,
	                timeout := T#1s,
	                mb := #mb );
	
	#siemens_PAC3200(unit := 255, // Device address. 
	                 mb := #mb);  // Same udt as for mb_client_ctrl above.
	
	// mb_master_ctrl and mb_buffer can be deleted to save memory
	// in the cpu. Those blocks are only for modbus rtu.
	
	
END_FUNCTION_BLOCK

DATA_BLOCK "start_example_mb_tcp_DB"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
NON_RETAIN
"start_example_mb_tcp"

BEGIN

END_DATA_BLOCK

ORGANIZATION_BLOCK "tcp_ob"
TITLE = "Main Program Sweep (Cycle)"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1

BEGIN
	// Program cycle OB
	
	// Open the block to see the example.
	
	"start_example_mb_tcp_DB"();
	
	
END_ORGANIZATION_BLOCK

