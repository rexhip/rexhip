FUNCTION_BLOCK "abb_266"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      mb_addr : UInt;
   END_VAR

   VAR_IN_OUT 
      mb_query : "mb_query";
   END_VAR

   VAR 
      s : "mb_station_udt";
      data : Struct
         "Differential Pressure" : Real;
         "Static Pressure" : Real;
         "Process Temperature" : Real;
         "Mass flow rate value" : Real;
         "Volume Flow rate value" : Real;
      END_STRUCT;
      settings : Struct
         Differential_Pressure_Units_Code : UInt := 12;
         Static_Pressure_Units_select_Code : UInt := 12;
         Temperature_Units_select_Code : UInt := 32;
      END_STRUCT;
   END_VAR


BEGIN
	// ABB - 266 MODBUS, multivariable transmitter.
	// Differential and Static Pressure, Temperature
	// Web: https://library.e.abb.com/public/
	//      9b5a1233ec79400387d5b4887002fb00/2105216MNAA.pdf
	
	"mb_station_header"(station := #s, mb_query := #mb_query);
	
	#mb_query.mb_addr := #mb_addr;
	
	#mb_query(mode := #mb_query.c.read.holding_reg,
	          data_addr := 401,
	          data_ptr := #data);
	
	#mb_query(mode := #mb_query.c.write.holding_reg,
	          data_addr := 481,
	          data_ptr := #settings);
	
	"mb_station_footer"(station := #s, mb_query := #mb_query);
	
	(*
	Differential_Pressure_Units_Code
	1 = inches of water
	8 = Millibar (ROM default)
	11 = Pascal
	12 = Kilopascal 
	
	Static Pressure Units select Code
	6 = PSIA
	7 = Bar (ROM default)
	12 = Kilopascal
	117 = Megapascal 
	
	Temperature Units select Code
	32 = Degree Celsius (ROM default)
	33 = Degree Fahrenheit
	34 = Degree Rankine
	35 = Degree Kelvin
	*)
END_FUNCTION_BLOCK

